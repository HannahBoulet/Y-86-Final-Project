# This program performs an insertion sort on a list of numbers.
# Note the sort is in-place. At the end there is a single, sorted list.
# If the program is correct, then the final memory dump should contain the 
# following:
# 100: 000000000000000a fffffffffffffff9 fffffffffffffffe 0000000000000002 
# 120: 0000000000000003 0000000000000005 0000000000000009 000000000000000c 
# 140: 000000000000000e 0000000000000017 000000000000001b 0000000000000000 
#
        .pos 0
        irmovq stack, %rsp   #initialize stack pointer
        irmovq list, %rdi    #address of first element
        irmovq len, %rax     #address of length of list
        mrmovq (%rax), %rax  #load length of list
        irmovq 8, %rsi       #offset of current element to insert
        irmovq 1, %rcx       #length of sorted list 
#  
# loop inserting each element into a sorted list
# first iteration: insert 3 into list containing 14 giving the list 3 14
# second iteration: insert 2 into the list containing 3 14 giving the list 2 3 14
# third iteration: insert 27 into the list contain 2 3 14 giving 2 3 14 27
# stop when the length of the sorted list is equal to the length of list
# 
# TODO: Provide missing code (see description below of insert function)
#
mainloop:
        call insert 
        irmovq $8, %rbx         # incrementing rsi
        addq %rbx, %rsi
        irmovq $1, %rbx         # incrementing rcx 
        addq %rbx, %rcx
        rrmovq %rcx, %rbx
        subq %rax, %rbx         # if rax = rcx 
        jne mainloop            # jump if not equal 
        halt
        
#
# insert function
# Takes the address of the first element in the list
# and the offset from that address of the new element to insert
# and swaps the values of consecutive elements until the new element
# is in the correct spot.
# For example, suppose current list is -2 2 3 14 27
# Next element to insert is 9 (which in memory is right after the 27)
# The insert function will loop:
# after first iteration the list will be: -2 2 4 14 9 27
# after second iteration the list will be: -2 2 4 9 14 27
# Now, the function is exited because the 9 is in the right spot (> 4)
#
# TODO: write the insert function
insert:
        rrmovq %rdi, %r8
        addq %rsi, %r8                  # get next element 

loop:
        mrmovq (%r8), %r9 
        mrmovq -8(%r8), %r10            # current element 
        rrmovq %r9, %r12
        subq %r10, %r12 
        jge done                         # if negative, new element in right spot
        rmmovq %r10, (%r8)               # swap 
        rmmovq %r9, -8(%r8)
        irmovq $8, %r13
        subq %r13, %r8 
        rrmovq %r8, %r14
        subq %rdi, %r14 
        je done                         # jump if equal
        jmp loop                        # continue looping until its equal

done:
        ret
#
#        list data
        .pos 0x100
len:    .quad 10 
list:   .quad 14
        .quad 3
        .quad 2
        .quad 27
        .quad -2
        .quad 12
        .quad 23
        .quad 5
        .quad -7
        .quad 9

#       stack    
        .pos 0x200
stack:  .quad 0    
